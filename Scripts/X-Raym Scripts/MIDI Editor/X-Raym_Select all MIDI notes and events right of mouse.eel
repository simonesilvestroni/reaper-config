// Select all MIDI notes and events right of mouse
// EEL Script for Reaper
// Author : X-Raym
// Author URI : http://extremraym.com
// Source : GitHub > X-Raym > EEL Scripts for Cockos REAPER
// Source URI : https://github.com/X-Raym/REAPER-EEL-Scripts
// Licence : GPL v3
// Release Date : 24-01-2015
// Version : 1.0
// Version Date : 03-02-2015
// Required : Reaper 5.0 pre 10; SWS 2.6.2

// Based on Select all MIDI notes and events right of cursor
// by spk77
// http://forum.cockos.com/showthread.php?p=1465307#post1465307

function select() 
  local(take, i, cursor_ppq_pos, notes, ccs, sysex, sel, muted, start, end, chan, pitch, vel, chanmsg, msg2, msg3, type)
(
  (take = MIDIEditor_GetTake(MIDIEditor_GetActive())) ? (
    MIDIEditor_OnCommand(MIDIEditor_GetActive(), 40214); // unselect all
    extension_api("BR_GetMouseCursorContext", #window, #segment, #details, char_sz);
    time = extension_api("BR_GetMouseCursorContext_Position");
    cursor_ppq_pos = MIDI_GetPPQPosFromProjTime(take, time);
    MIDI_CountEvts(take, notes, ccs, sysex);
    
    i = 0;
    loop(notes,
      MIDI_GetNote(take, i, sel, muted, start, end, chan, pitch, vel);
      start >= cursor_ppq_pos ? MIDI_SetNote(take, i, 1, muted, start, end, chan, pitch, vel);
      i += 1;
    );
    
    i = 0;
    loop(ccs,
      MIDI_GetCC(take, i, sel, muted, start, chanmsg, chan, msg2, msg3);
      start >= cursor_ppq_pos ? MIDI_SetCC(take, i, 1, muted, start, chanmsg, chan, msg2, msg3);
      i += 1;
    );
    
    i = 0;
    loop(sysex,
      MIDI_GetTextSysexEvt(take, i, sel, muted, start, type, #msg);
      start >= cursor_ppq_pos ? MIDI_SetTextSysexEvt(take, i, 1, muted, start, type, #msg);
      i += 1;
    );
  );
  Undo_OnStateChange("Select events right of cursor");
);

select();