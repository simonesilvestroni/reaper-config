/**
 * ReaScript Name: Snap selected items to the previous item end on their track
 * Description: Snap selected items to the previous item end on their track
 * Instructions: Select items. Execute the script.
 * Author: X-Raym
 * Author URI: http://extremraym.com
 * Repository: GitHub > X-Raym > EEL Scripts for Cockos REAPER
 * Repository URI: https://github.com/X-Raym/REAPER-EEL-Scripts
 * File URI: 
 * Licence: GPL v3
 * Forum Thread: ReaScript: EEL Script. Item_Connect
 * Forum Thread URI: http://forum.cockos.com/showthread.php?t=157907
 * REAPER: 4.77
 * Extensions: None
 * Version: 1.0.1
*/

/**
 * Changelog:
 * v1.0.1 (2018-03-19)
	# User Config Area for offset
 * v1.0 (2015-03-29)
	+ Initial Release
 */

/* USER CONFIG AREA */

offset = 0; // Offset time (seconds)

/* END OF USER CONFIG AREA */


function main()
(
	// RESET
	//ShowConsoleMsg("");

	// COUNT SEL ITEMS
	count_sel_items = CountSelectedMediaItems(0);
	//MsgD("Number of selected items: ", count_sel_items);

	i = 0;
	loop(count_sel_items,

		item = GetSelectedMediaItem(0, i);
		item_pos = GetMediaItemInfo_Value(item, "D_POSITION");

		new_item_pos = item_pos +1;

		item_id = GetMediaItemInfo_Value(item, "IP_ITEMNUMBER");
		
		item_id != 0 ? (

			track = GetMediaItemTrack(item);

			previous_item_id = item_id - 1;

			previous_item = GetTrackMediaItem(track, previous_item_id);
			previous_item_pos = GetMediaItemInfo_Value(previous_item, "D_POSITION");
			previous_item_len = GetMediaItemInfo_Value(previous_item, "D_LENGTH");
			previous_item_end = previous_item_pos + previous_item_len;

			//MsgF("Previous item end: ", previous_item_end);

			SetMediaItemInfo_Value(item, "D_POSITION", previous_item_end + offset);

		);

		i = i+1;

	); // END of loop through selected items

); // END of main

PreventUIRefresh(1);
main(); // Execute main
PreventUIRefresh(-1);
UpdateArrange();