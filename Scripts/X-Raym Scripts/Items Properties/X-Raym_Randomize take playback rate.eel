/**
 * ReaScript Name: Randomize take playback rate
 * Description: This script is a demonstration of how we can randomize take playback rate. It is not meant to be use without modifications of the R value.
 * Instructions: Here is how to use it. (optional)
 * Author: X-Raym
 * Author URI: http://extremraym.com
 * Repository: GitHub > X-Raym > EEL Scripts for Cockos REAPER
 * Repository URI: https://github.com/X-Raym/REAPER-EEL-Scripts
 * File URI: https://github.com/X-Raym/REAPER-EEL-Scripts/scriptName.eel
 * Licence: GPL v3
 * Forum Thread: Scripts (EEL): Randomize Take Playback Rate
 * Forum Thread URI: http://forum.cockos.com/showthread.php?p=1520462
 * REAPER: 4.76
 * Extensions: None
 * Version: 1.1
*/
 
/**
 * Changelog:
 * v1.1 (2015-05-12)
  + Randomize from snap offset position
  + Support for fades
 * v1.0 ()
  + Initial Release
 */

function rand_take_pb_rate() local(i, item, take)
(
  i = 0;
  loop(CountSelectedMediaItems(0), 
    (item = GetSelectedMediaItem(0, i)) ? (
      (take = GetActiveTake(item)) ? (

        R = rand(2);
        // USER INPUT => MODIFY R HERE
        // R = 
        // USER INPUT => END OF R MODIFICATIONS

        // INITIAL ITEM INFOS
        init_fadein = GetMediaItemInfo_Value(item, "D_FADEINLEN");
        init_fadeout = GetMediaItemInfo_Value(item, "D_FADEOUTLEN");
        init_rate = GetMediaItemTakeInfo_Value(take, "D_PLAYRATE");
        init_position = GetMediaItemInfo_Value(item, "D_POSITION");
        init_length = GetMediaItemInfo_Value(item, "D_LENGTH");
        init_snap = GetMediaItemInfo_Value(item, "D_SNAPOFFSET");

        // USE SOURCE AS REFRENCE
        src_snap = init_snap * init_rate;
        src_fadein = init_fadein * init_rate;
        src_fadeout = init_fadeout * init_rate;
        src_length = init_length * init_rate;

        // SET VALUES
        SetMediaItemTakeInfo_Value(take, "D_PLAYRATE", R);

        new_snap = src_snap / R;
        SetMediaItemInfo_Value(item, "D_SNAPOFFSET", new_snap);
        
        snap_dif = init_snap - new_snap;
        SetMediaItemInfo_Value(item, "D_POSITION", init_position + snap_dif);
        
        SetMediaItemInfo_Value(item, "D_LENGTH", src_length / R);
        
        init_fadein > 0 ? (
          SetMediaItemInfo_Value(item, "D_FADEINLEN", src_fadein / R);
        );

        init_fadein > 0 ? (
          SetMediaItemInfo_Value(item, "D_FADEOUTLEN", src_fadeout / R);
        );
      );
    );
    i += 1;
  );
  UpdateArrange(); // refresh arrange view
  Undo_OnStateChange("Randomize take playback rate");
);

PreventUIRefresh(1);
rand_take_pb_rate();
PreventUIRefresh(-1);
